PREFIX dct:    <http://purl.org/dc/terms/>
prefix fabio:  <http://purl.org/spar/fabio/>
prefix frbr:   <http://purl.org/vocab/frbr/core#>
PREFIX oa:     <http://www.w3.org/ns/oa#>
prefix rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>

SELECT ?document ?title ?date ?publisher ?lang (group_concat(distinct ?author, "$") as ?authors) (group_concat(distinct ?matchedEntity, "$$") as ?matchedEntities)
FROM <http://ns.inria.fr/d2kab/graph/wheatgenomicsslkg>
FROM <http://purl.obolibrary.org/obo/ncbitaxon/ncbitaxon.owl>
WHERE {
    ?document a fabio:ResearchPaper.
    OPTIONAL { ?document dct:title ?title. FILTER isLiteral(?title) }
    OPTIONAL { ?document dct:creator ?author. }
    OPTIONAL { ?document dct:issued ?date. }
    OPTIONAL { ?document dct:source ?publisher. }
    OPTIONAL { ?document dct:language ?lang. }

    ?a1 oa:hasTarget [ oa:hasSource ?source ];
        oa:hasBody ?entityUri.
    ?source frbr:partOf+ ?document.

    ?entityUri rdfs:subClassOf* <{id}>; rdfs:label ?entityLabel.

    BIND(concat(?entityUri, '$', ?entityLabel) as ?matchedEntity)

} group by ?document ?title ?date ?publisher ?lang limit 20000