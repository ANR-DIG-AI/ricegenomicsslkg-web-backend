prefix oa:     <http://www.w3.org/ns/oa#>
prefix schema: <http://schema.org/>
prefix gn:          <http://www.geonames.org/ontology#>
prefix wgs84_pos:   <http://www.w3.org/2003/01/geo/wgs84_pos#>

select distinct ?entityUri ?name ?officialName ?latitude ?longitude ?altitude ?alternateName ?nameParentFeature ?nameParentCountry
where {
    # Find the named entities on that article and keep only those in Geonames
    []  a oa:Annotation;
    schema:about <{id}>;   # in all the article, wether in the title, abstract or body
    oa:hasBody ?entityUri.
    FILTER( contains(str(?entityUri), "sws.geonames.org") )

    # Get additional information about the named entities
    ?entityUri
        gn:name         ?name;
        wgs84_pos:lat   ?latitude;
        wgs84_pos:long  ?longitude.
    OPTIONAL { ?entityUri gn:officialName   ?officialName }.
    OPTIONAL { ?entityUri wgs84_pos:alt     ?altitude }.
    OPTIONAL { ?entityUri gn:alternateName  ?alternateName }.
    OPTIONAL { ?entityUri gn:parentCountry  [ gn:name ?nameParentCountry ] }.
    OPTIONAL { ?entityUri gn:parentFeature  [ gn:name ?nameParentFeature ] }.
}