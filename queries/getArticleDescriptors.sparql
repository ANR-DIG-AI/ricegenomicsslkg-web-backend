PREFIX bibo:   <http://purl.org/ontology/bibo/>
PREFIX dct:    <http://purl.org/dc/terms/>
PREFIX fabio:  <http://purl.org/spar/fabio/>
PREFIX oa:     <http://www.w3.org/ns/oa#>
PREFIX prov:   <http://www.w3.org/ns/prov#>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos:   <http://www.w3.org/2008/05/skos-xl>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>

SELECT DISTINCT ?entityUri ?entityLabel ?isGeographicalDescriptor (GROUP_CONCAT(?descriptorType; separator=',') as ?types)
FROM <http://data-issa.cirad.fr/graph/documents>
FROM NAMED <http://data-issa.cirad.fr/graph/thematic-descriptors>
FROM NAMED <http://data-issa.cirad.fr/graph/annif-descriptors>
FROM <http://agrovoc.fao.org/graph>
WHERE {
    <{id}> a prov:Entity.

    { GRAPH <http://data-issa.cirad.fr/graph/thematic-descriptors> {
        ?descriptor
            oa:hasTarget <{id}>;
            oa:hasBody ?entityUri.
        bind("human" as ?descriptorType)
    }}

    UNION

    { GRAPH <http://data-issa.cirad.fr/graph/annif-descriptors> {
        ?descriptor
            oa:hasTarget <{id}>;
            oa:hasBody ?entityUri.
        bind("computed" as ?descriptorType)
    }}

    OPTIONAL {
        # Find labels that may be gven with various properties depending on the data source
        ?entityUri rdfs:label|skos:prefLabel|(skosxl:prefLabel/skosxl:literalForm)|dct:title|schema:name ?entityLabel.
        FILTER langMatches(lang(?entityLabel), "en")
    }

    OPTIONAL {
        ?entityUri <http://aims.fao.org/aos/agrontology#isPartOfSubvocabulary> ?subVocabulary .
        BIND ( REGEX(?subVocabulary, "^Geographical", "i") as ?isGeographicalDescriptor)
    }

} GROUP BY ?entityUri ?entityLabel ?isGeographicalDescriptor
  ORDER BY DESC(?types)
