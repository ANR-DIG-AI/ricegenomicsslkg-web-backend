PREFIX oa:          <http://www.w3.org/ns/oa#>
PREFIX schema:      <http://schema.org/>
PREFIX gn:          <http://www.geonames.org/ontology#>
PREFIX wgs84_pos:   <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX prov:   <http://www.w3.org/ns/prov#>

SELECT ?entityUri ?name ?latitude ?longitude ?altitude ?officialName ?nameParentCountry (group_concat(distinct ?alternateName ; separator=", ") as ?alternateNames)
FROM <http://geonames.org/graph>
FROM <http://data-issa.cirad.fr/graph/geographic-nes>
WHERE {
    <{id}>
        a prov:Entity;
        ^schema:about [ oa:hasBody ?entityUri ].

    ?entityUri
        gn:name         ?name;
        wgs84_pos:lat   ?latitude;
        wgs84_pos:long  ?longitude.
        OPTIONAL {
            ?entityUri gn:officialName ?officialName
            FILTER langMatches(lang(?officialName), "en")
        }.
        OPTIONAL {
            ?entityUri gn:alternateName ?alternateName.
            FILTER langMatches(lang(?alternateName), "en")
        }.
        OPTIONAL { ?entityUri wgs84_pos:alt     ?altitude }.
        OPTIONAL { ?entityUri gn:parentCountry  [ gn:name ?nameParentCountry ] }.
} group by ?entityUri ?name ?latitude ?longitude ?altitude ?officialName ?nameParentCountry
